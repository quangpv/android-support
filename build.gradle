// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.publishVersion = '1.0'
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.0"
        classpath 'com.github.kezong:fat-aar:1.3.6'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    Project project = it
    String name = project.name
    String supportPrefix = "support-"

    if (!name.contains(supportPrefix)) return
    String artifactName = name.replace(supportPrefix, "").replace("-", "")

    println("Add config to publish module ${artifactName}")
    apply plugin: 'maven-publish'

    afterEvaluate {
        tasks.withType(JavaCompile) {
            options.encoding = "UTF-8"
        }

        task sourcesJar(type: Jar) {
            from android.sourceSets.main.java.srcDirs
            archiveClassifier.set('sources')
        }

        task javadoc(type: Javadoc) {
            failOnError false
            source = android.sourceSets.main.java.sourceFiles
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            configurations.implementation.setCanBeResolved(true)
            configurations.api.setCanBeResolved(true)
            classpath += configurations.implementation
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            archiveClassifier.set('javadoc')
            from javadoc.destinationDir
        }

        artifacts {
            archives sourcesJar
            archives javadocJar
        }

        afterEvaluate {
            publishing {
                publications {
                    aar(MavenPublication) {
                        groupId = 'com.github.quangpv'
                        version = "$publishVersion"
                        artifactId = "$artifactName"
                        afterEvaluate {
                            artifact bundleReleaseAar
                            artifact sourcesJar
                            artifact javadocJar
                        }
                    }
                }
            }
        }
    }
}